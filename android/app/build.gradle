apply plugin: 'com.android.application'

static def getVersionCodeFromVersionName(String versionName) {
    def versionParts = versionName.split("\\.")
    while (versionParts.size() < 3) {
        versionParts += "0"
    }
    return versionParts[0].toInteger() * 10000 + versionParts[1].toInteger() * 100 + versionParts[2].toInteger()
}

android {
    namespace "tv.megacubo.app"
    compileSdk rootProject.ext.compileSdkVersion
    compileSdkVersion 34
    defaultConfig {
        applicationId "tv.megacubo.app"
        minSdkVersion 24
        targetSdkVersion 34
        versionName '17.5.5'
        versionCode getVersionCodeFromVersionName(versionName)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
        vectorDrawables.useSupportLibrary = true
        resConfigs "en"
    }
    buildTypes {
        release {
            minifyEnabled true // fine
            shrinkResources true // fine
            postprocessing {
                proguardFiles = []
                removeUnusedCode false // villain
                removeUnusedResources true // fine
                obfuscate false // villain
                optimizeCode true // fine
            }
        }
    }
    splits {
        abi {
            enable false
            reset()
            include 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
            pickFirsts += [
                /*
                'lib/x86/libc++_shared.so',
                'lib/x86_64/libc++_shared.so',
                */
                'lib/armeabi-v7a/libnative-lib.so',
                'lib/armeabi-v7a/libc++_shared.so',
                'lib/arm64-v8a/libnative-lib.so',
                'lib/arm64-v8a/libc++_shared.so'
            ]
            excludes += ['**/*~', 'lib/x86/**', 'lib/x86_64/**']
        }
        resources {
            excludes += ['META-INF/NOTICE', 'META-INF/LICENSE', 'META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/ASL2.0', '**/.DS_Store', '**/thumbs.db', '**/*.bak', '**/*~']
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs', '../capacitor-cordova-android-plugins/src/main/libs', 'libs']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
